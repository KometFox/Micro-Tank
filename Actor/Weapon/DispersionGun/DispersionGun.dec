//

ACTOR MT_DispersionGun : Weapon 
{

  +WEAPON.AMMO_OPTIONAL
  +WEAPON.ALT_AMMO_OPTIONAL  
  +WEAPON.CHEATNOTWEAPON
  +INVENTORY.UNDROPPABLE
  +WEAPON.NOALERT
  +WEAPON.DONTBOB
  Weapon.SlotNumber 3
  Weapon.SlotPriority 0
  Inventory.PickupMessage "$GOTPLASMA"
  Tag "Dispersion Gun"
  Weapon.AmmoUse1 0
  Weapon.AmmoType1 MT_DispersionCell
  Weapon.AmmoUse2 2
  Weapon.AmmoType2 MT_DispersionCell 
  States
  {
  Ready:
    TNT1 A 0
    DISP AAAAAAAAAA 1 A_WeaponReady
    DISP A 1 A_GiveInventory("MT_DispersionCell",10)
    Loop
  Deselect:
    DISP A 0 A_TakeInventory("MT_DispersionGun_IsSelected",1)
    DISP AAAAA 0 A_Lower
    DISP A 1 A_Lower
    Loop
  Select:
    DISP A 0 A_GiveInventory("MT_DispersionGun_IsSelected",1)
    DISP AAAAA 0 A_Raise
    DISP A 1 A_Raise
    Loop
  AltFire:
    TNT1 A 0 A_JumpIfInventory("MT_DispersionCell",4,"AltFire_1")
	Goto AltFire_2
  AltFire_1:
    DISP A 0 A_GunFlash
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small"        ,0,1,6+12)
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small_Trail"  ,0,0,6+12)
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small_Trail2" ,0,0,6+12)		
	DISP A 4
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small"        ,0,1,-6+12)
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small_Trail"  ,0,0,-6+12)
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small_Trail2" ,0,0,-6+12)		
	DISP A 4 
    DISP A 0 A_ReFire 
    Goto Ready
  AltFire_2:
    DISP A 0 A_GunFlash
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small"        ,0,1,6+12)
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small_Trail"  ,0,0,6+12)
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small_Trail2" ,0,0,6+12)		
	DISP A 12
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small"        ,0,1,-6+12)
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small_Trail"  ,0,0,-6+12)
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Small_Trail2" ,0,0,-6+12)		
	DISP A 12
    DISP A 0 A_ReFire 
    Goto Ready	
  Hold:
   TNT1 A 0 A_JumpIfInventory("MT_DispersionGun_Func_Charge",1,"Fire_Charge_AmmoCheck")
   TNT1 A 0 A_JumpIfInventory("MT_DispersionGun_Func_NoCharge",1,"Fire_1NC_AmmoCheck")
   Goto Ready	
  Fire:
   TNT1 A 0 A_JumpIfInventory("MT_DispersionGun_Func_Charge",1,"Fire_Charge2")
   TNT1 A 0 A_JumpIfInventory("MT_DispersionGun_Func_NoCharge",1,"Fire_1NC_AmmoCheck")
   Goto Ready
   
  Fire_Charge_AmmoCheck:
    TNT1 A 0 A_JumpIfInventory("MT_DispersionCell",2,"Fire_Charge")
	Goto Ready
  Fire_Charge:
    DISP A 0 A_PlaySound("DispPowe")
	Goto Fire_Charging
  Fire_Charging:
    DISP A 6
	DISP A 0 A_GiveInventory("MT_DispersionGun_Charge",2)
	DISP A 0 A_TakeInventory("MT_DispersionCell",2)
	DISP A 0 A_Refire("Fire_Charging")
	Goto Ready

/////Charged Firing
  Fire_Charge2:
   	DISP A 0 A_JumpIfInventory("MT_DispersionGun_Charge",50,"Fire_5")
	DISP A 0 A_JumpIfInventory("MT_DispersionGun_Charge",40,"Fire_4")
	DISP A 0 A_JumpIfInventory("MT_DispersionGun_Charge",30,"Fire_3")
	DISP A 0 A_JumpIfInventory("MT_DispersionGun_Charge",20,"Fire_2")
	DISP A 0 A_JumpIfInventory("MT_DispersionGun_Charge",10,"Fire_1")		
    DISP A 0 A_Refire("Fire_Charging")
    Goto Ready	
  Fire_5:
    DISP A 0 A_GunFlash
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Charge5_Large",0,1,4)
	DISP A 0 A_TakeInventory("MT_DispersionGun_Charge",50)
	DISP A 12 A_StopSound(1)
    Goto Ready
  Fire_4:
    DISP A 0 A_GunFlash
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Charge4_Large",0,1,4)
	DISP A 0 A_TakeInventory("MT_DispersionGun_Charge",40)
	DISP A 12 A_StopSound(1)
    Goto Ready		
  Fire_3:
    DISP A 0 A_GunFlash
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Charge3_Large",0,1,4)
	DISP A 0 A_TakeInventory("MT_DispersionGun_Charge",30)
	DISP A 12 A_StopSound(1)
    Goto Ready
  Fire_2:
    DISP A 0 A_GunFlash
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Charge2_Large",0,1,4)
	DISP A 0 A_TakeInventory("MT_DispersionGun_Charge",20)
	DISP A 12 A_StopSound(1)
    Goto Ready			
  Fire_1:
    DISP A 0 A_GunFlash
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Charge1_Large",0,1,4)
	DISP A 0 A_TakeInventory("MT_DispersionGun_Charge",10)
	DISP A 12 A_StopSound(1)
    Goto Ready

	
/////Normal Firing
  Fire_1NC_AmmoCheck:
    TNT1 A 0 A_JumpIfInventory("MT_DispersionCell",10,"Fire_1NC")
	Goto Fire_1NCSlow
  Fire_1NC:
    DISP A 0 A_GunFlash
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Charge1_Large",0,1,4)
	DISP A 0 A_StopSound(1)  
	DISP A 0 A_TakeInventory("MT_DispersionCell",10)
    DISP A 15
	Goto Ready	
  Fire_1NCSlow:
    DISP A 0 A_GunFlash
	DISP A 0 A_FireCustomMissile("MT_DispersionBall_Charge1_Large",0,1,4)
	DISP A 0 A_StopSound(1)  
    DISP A 32
	Goto Ready		
  Flash:
    TNT1 A 0 
    DISP A 4  
    Goto LightDone
  Spawn:
    DISP A -1
    Stop
  }
}

ACTOR MT_DispersionGun_Charge : Inventory
{
  +INVENTORY.UNDROPPABLE
  //+INVENTORY.UNTOSSABLE
  Inventory.Amount 50
  Inventory.MaxAmount 50
}

////////////////Ammo
  ACTOR MT_DispersionCell : Ammo 
{
  Inventory.PickupMessage "Picked Up: 25 units of Energy Cell"
  Inventory.Amount 50
  Inventory.MaxAmount 250
  Ammo.BackpackAmount 0
  Ammo.BackpackMaxAmount 500
  Inventory.Icon "ENCEA0"
  Scale 1.5
  States
  {
  Spawn:
    ENCE A -1
    Stop
  }
}




////////////////Projectile
Actor MT_DispersionBall_Small : PlasmaBall
{
SeeSound "DispShot"
DeathSound "LasH1"
Speed 60
Scale 1.1
DamageType Energy
  States
  {
  Spawn:
    PLSS AB 6 Bright
    Loop
  Death:
    TNT1 A 0 A_SpawnItemEx("MT_CEG_DispersionGun_MediumSpark",0,0,0,+random(-2,2),+random(-2,2),+random(-2,2)) 
    TNT1 AAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_SmallSpark",0,0,0,+random(-3,3),+random(-3,3),+random(-3,3)) 
    Stop
  }
}

Actor MT_DispersionBall_Small_Trail : MT_DispersionBall_Small
{
SeeSound ""
DeathSound ""
Speed 59
Damage 0
Scale 0.8
  States
  {
  Spawn:
    PLSS AB 6 Bright
    Loop
  Death:
    TNT1 A 0  
    Stop
  }
}

Actor MT_DispersionBall_Small_Trail2 : MT_DispersionBall_Small_Trail
{
SeeSound ""
DeathSound ""
Speed 58
Damage 0
Scale 0.5
}

Actor MT_DispersionBall_Charge1_Large : PlasmaBall
{
SeeSound "DispShot"
DeathSound "LasH1"
Speed 45
Scale 1.25
DamageType Energy
  States
  {
  Spawn:
    PLSS AB 6 Bright
    Loop
  Death:
    TNT1 AAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_BigSpark",0,0,0,+random(-5,5),+random(-5,5),+random(-5,5)) 
    TNT1 AAAAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_SmallSpark",0,0,0,+random(-8,8),+random(-8,8),+random(-8,8)) 
    TNT1 A 0 A_Explode(70,60)
	Stop
  }
}

Actor MT_DispersionBall_Charge2_Large : MT_DispersionBall_Charge1_Large
{
  Scale 1.5
  States
  {
  Death:
    TNT1 AAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_BigSpark",0,0,0,+random(-5,5),+random(-5,5),+random(-5,5)) 
    TNT1 AAAAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_SmallSpark",0,0,0,+random(-8,8),+random(-8,8),+random(-8,8)) 
    TNT1 A 1 A_Explode(84,66)
	Stop	
  }
}


Actor MT_DispersionBall_Charge3_Large : MT_DispersionBall_Charge1_Large
{
  Scale 1.75
  States
  {
  Death:
    TNT1 A 1 A_Explode(101,73)
    TNT1 AAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_BigSpark",0,0,0,+random(-5,5),+random(-5,5),+random(-5,5)) 
    TNT1 AAAAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_SmallSpark",0,0,0,+random(-8,8),+random(-8,8),+random(-8,8)) 
    Stop
  }
}


Actor MT_DispersionBall_Charge4_Large : MT_DispersionBall_Charge1_Large
{
  Scale 2
  States
  {
  Death:
    TNT1 A 1 A_Explode(121,80)
    TNT1 AAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_BigSpark",0,0,0,+random(-5,5),+random(-5,5),+random(-5,5)) 
    TNT1 AAAAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_SmallSpark",0,0,0,+random(-8,8),+random(-8,8),+random(-8,8)) 
    Stop
  }
}

Actor MT_DispersionBall_Charge5_Large : MT_DispersionBall_Charge1_Large
{
  Scale 2.5
  States
  {
  Death:
   TNT1 A 1 A_Explode(145,88)
   TNT1 AAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_BigSpark",0,0,0,+random(-5,5),+random(-5,5),+random(-5,5)) 
   TNT1 AAAAAA 0 A_SpawnItemEx("MT_CEG_DispersionGun_SmallSpark",0,0,0,+random(-8,8),+random(-8,8),+random(-8,8)) 
   Stop
  }
}

/////////Particles


ACTOR MT_CEG_DispersionGun_BigSpark : Particle
{
  Height 4
  Radius 4
  speed 0.005
  Gravity 0
  Scale 1.3
  renderstyle Add
  -NOINTERACTION
  alpha 1
  States
  {
  Spawn:
    APP4 A 2 Bright
    APP4 BBCCDDEEFFGG 1 Bright A_Fadeout(0.029)  
    Goto Death
  Death:
    TNT1 A 1
    Stop
  }
}

ACTOR MT_CEG_DispersionGun_SmallSpark : MT_CEG_DispersionGun_BigSpark
{
  Scale 0.2
}

ACTOR MT_CEG_DispersionGun_MediumSpark : MT_CEG_DispersionGun_BigSpark
{
  Scale 0.5
}

